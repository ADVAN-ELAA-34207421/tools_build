//
// A basic Android application that follows all the conventions
//
buildscript {
    repositories {
        maven { url '../../repo' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.3-SNAPSHOT'
    }
}
apply plugin: 'android'

android {
    target = "android-15"

    sourceSets {
        main {
            manifest {
                // there's only ever one file so srcFile replaces it.
                srcFile 'AndroidManifest.xml'
            }
            java {
                // writing:
                //    srcDir 'src'
                // would *add* to the default folder so we use a different syntax
                srcDirs = ['src']
                exclude 'some/unwanted/package/**'
            }
            res {
                srcDirs = ['res']
            }
            assets {
                srcDirs = ['assets']
            }
            resources {
                srcDirs = ['src']
            }
        }

        // this moves src/test to tests so all folders follow:
        // tests/java, tests/res, tests/assets, ...
        // This is a *reset* so it replaces the default paths
        test.setRoot('tests')

        // Could also be done with:
        //main.manifest.srcFile 'AndroidManifest.xml'
        //main.java.srcDir 'src'
        //main.res.srcDir 'res'
        //main.assets.srcDir 'assets'
        //main.resources.srcDir 'src'
        //test.java.srcDir 'tests/src'
    }
}